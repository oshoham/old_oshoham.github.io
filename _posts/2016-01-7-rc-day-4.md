---
layout: post
title:  "RC: Day 4 - Reverse-Engineering a Fragment Shader"
date:   2016-01-07 16:18:20
categories: rc
---

After reading up on WebGL for a while, I ended up on [Shadertoy](shadertoy.com) and [GLSL Sandbox](glslsandbox.com), looking at other people's cool fragment shaders and wondering, "How do any of these work???". So, today I picked a simple-ish shader and decided to go through it, line by line, until I understood what it was doing.

Many hours later, and with the help of Andrew A. and Andrew D., I ended up with this:

![whiteboard madness]({{ site.url }}/assets/rc-day-4/fragment_shader_whiteboard.jpg "Glory.")

But I was worried that I'd quickly forget all the explanations that seemed really clear at the time, so I decided to write my notes up as a blog post. Let's do this.

**Note:** I'm assuming that you have a basic understanding of what WebGL is and how it works. If you're totally new to WebGL, I'd recommend checking out [this StackOverflow question](https://stackoverflow.com/questions/7328472/how-webgl-works) or [WebGL Fundamentals](http://webglfundamentals.org/webgl/lessons/webgl-how-it-works.html) before reading on.

Here's our motivation:

{% include rc-day-4/fragment-shader-demo-canvas.html demoNumber=1 %}

And here's the fragment shader that draws this cool circle-y thing, copied verbatim from [http://glslsandbox.com/e#29842.0](http://glslsandbox.com/e#29842.0):

{% highlight glsl %}
precision mediump float;
uniform vec2 resolution;
uniform float time;

void main() {
  vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);
  vec3 destColor = vec3(1.0, 0.7, 0.5);
  float f = 0.0;
  for(float i = 0.0; i < 10.0; i++) {
      float s = sin(time + i * 0.628318) * 0.5;
      float c = cos(time + i * 0.628318) * 0.5;
      f += 0.0025 / abs(length(p + vec2(c, s)) - 0.5);
  }
  gl_FragColor = vec4(vec3(destColor * f), 1.0);
}
{% endhighlight %}

At first glance, this seems pretty confusing (or at least it did to me :sweat_smile:). Single-character variable names, magic numbers, math that requires a solid understanding of high school geometry...what's going on here? Well...


## Variables

{% highlight glsl %}
precision mediump float;
uniform vec2 resolution;
uniform float time;
{% endhighlight %}

`precision mediump float;` tells the GPU to use medium precision when calculating floating point values. The other options are `lowp` and `highp`; a higher precision results in more accurate but slower calculations.

`resolution` and `time` are uniforms that we pass in to the fragment shader when we render our scene. In the examples in this post, `resolution` is set to (`canvas.width`, `canvas.height`), while `time` is set to the current duration that our scene has been animating for, divided by 100. If we wanted to adjust the speed at which the scene animates, we could change the factor by which we scale `time`.

## Converting Coordinates

{% highlight glsl %}
vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);
{% endhighlight %}

`gl_FragCoord` is a special shader variable that is always (and only) present in fragment shaders. The value of `gl_FragCoord` is the coordinates $(x, y, z, 1/w)$ of the center of the pixel that the current fragment is being rendered to, in _window coordinates_, which range from $(0.0, 0.0)$ to $(windowWidth, windowHeight)$. Since we're drawing in 2D, we only care about the $(x, y)$ values.

This line converts the position of the pixel (hence the variable name `p`) to _normalized device coordinates_, which range from $(-1.0, -1.0)$ to $(1.0, 1.0)$. As an example, suppose we're drawing on an 800x600 canvas, and we're rendering the fragment that falls over the bottom-left corner of the canvas, which would have window coordinates of $(0.5, 0.5)$:

$$p = ((0.5, 0.5) * 2.0 - (800, 600)) / 600 = (-0.99875, -0.99875)$$

Why do we want the pixel position in NDC? Well, WebGL draws pixels in terms of NDC (the vertex shader produces NDC), but in this case we're basically doing all the work of drawing the geometry in our scene in our fragment shader, so if we want to draw in the correct locations on our canvas, we'll need assign color values to fragments based on pixel positions in NDC space.

## Color

{% highlight glsl %}
vec3 destColor = vec3(1.0, 0.7, 0.5);
{% endhighlight %}

Here, we're just setting up a color vector with the RGB values for light pink. Technically, `gl_FragColor` expects an RGBA `vec4`, not an RGB `vec3`, but since the alpha (opacity) value is going to be 1.0 and we'll be multiplying this vector by a scalar value later on, it's simpler to leave the alpha value off for now.

## Drawing a Circle

{% highlight glsl %}
for(float i = 0.0; i < 10.0; i++) {
  float s = sin(time + i * 0.628318) * 0.5;
  float c = cos(time + i * 0.628318) * 0.5;
  f += 0.0025 / abs(length(p + vec2(c, s)) - 0.5);
}
gl_FragColor = vec4(vec3(destColor * f), 1.0);
{% endhighlight %}

There's a fair amount going on here. Let's start with the last line.

We're setting `gl_FragColor` (the special variable that WebGL uses to set the color of the fragment being processed) to `destColor * f`. You can think of multiplying by `f` as applying a mask to `destColor`. Any coordinates that do not lie on the equation described by `f` will be black (because multiplying by 0 sets `gl_FragColor` to (0.0, 0.0, 0.0, 1.0)), and any fragments that do will be pink (because of the multiplicative identity).

Back to the geometry. Let's ignore the for-loop, `s`, and `c` for a moment and simplify things down to this:

{% highlight glsl %}
float f = 0.0025 / abs(length(p) - 0.5);
{% endhighlight %}

This produces a single circle drawn around $(0, 0)$:

{% include rc-day-4/fragment-shader-demo-canvas.html demoNumber=2 %}

But how? Let's start with `length(p)` and work outwards. According to the [OpenGL docs](https://www.opengl.org/sdk/docs/man/html/length.xhtml),

$$
length(vector) = \sqrt{vector.x^2 + vector.y^2 + \dots}
$$

The [equation for graphing a circle](https://en.wikipedia.org/wiki/Circle#Equations) centered at the origin, i.e. $(0, 0)$, is $x^2 + y^2 = r^2$, which could also be written as $x^2 + y^2 - r^2 = 0$.

Using these definitions, we can rewrite $length(p) - 0.5$ as $p.x^2 + p.y^2 - 0.5^2$, which is the equation for a circle with a radius of 0.25.

However, `float f = length(p) - 0.5;` produces positive non-zero values for _all_ coordinates on or outside of the circle, so if we stop here, we get the following:

{% include rc-day-4/fragment-shader-demo-canvas.html demoNumber=3 %}

`float f = 0.0025 / (length(p) - 0.5` produces positive non-zero values for all coordinates that lie on or between the circle with radius 0.25 and the circle with radius 0.2525. You can think of 0.0025 as the radius of the ring comprised of these two circles. However, this produces a ring with a jagged inner edge, as you can see below:

{% include rc-day-4/fragment-shader-demo-canvas.html demoNumber=4 %}

Dividing by `abs(length(p) - 0.5)` doubles the width of the ring, which smoothes out its inner edge, by also including coordinates that fall between the circle with radius 0.2475 and the circle with radius 0.2525.

## Trigonometry

Now that we understand how we're drawing a circle, let's add `s`, `c`, and `time`:

{% highlight glsl %}
float s = sin(time) * 0.5;
float c = cos(time) * 0.5;
float f = 0.0025 / abs(length(p + vec2(c, s)) - 0.5);
{% endhighlight %}

If you remember your high school geometry, given some angle $t$, $(cos(t), sin(t))$ are the $(x, y)$ coordinates at the intersection of the unit circle and a line that makes an angle of degree $t$ with the positive $x$-axis. As you vary $t$, you get a series of coordinates that define a rotation around edge of the unit circle.

![sin and cos on the unit circle]({{ site.url }}/assets/rc-day-4/sin_cos_unit_circle.png "(cos(t), sin(t)) are (x, y) coordinates at the intersection of the unit circle and a line making an angle of t with the positive x-axis."){:.center-image}

Here, we're using `time` as our angle `t`. An angle in radians greater than 2π wraps back around to 0, so as `time` increases, `vec2(c, s)` continues to describe a rotation around the unit circle. Well, not exactly the unit circle - because we multiply `sin(time)` and `cos(time)` by 0.5, `vec2(c, s)` actually describes a rotation around a circle with radius 0.5, and moves half as quickly.

Adding `vec2(c, s)` to `p` translates `p.x` by `c` and `p.y` by `s`, which causes our circle to orbit:

{% include rc-day-4/fragment-shader-demo-canvas.html demoNumber=5 %}

Okay, now back to the real code.

{% highlight glsl %}
for(float i = 0.0; i < 10.0; i++) {
  float s = sin(time + i * 0.628318) * 0.5;
  float c = cos(time + i * 0.628318) * 0.5;
  f += 0.0025 / abs(length(p + vec2(c, s)) - 0.5);
}
{% endhighlight %}

Each iteration of the loop draws an additional circle by adding `i * 0.628318` to the angle passed in to `sin` and `cos`, which offsets the position of the circle by `i * 0.628318`, and incrementing `f` by the circle formula.

Almost every pixel on the canvas will only fall on one of the 10 circles, so even though we increment `f` on every iteration of the loop, the value of `f` will still almost always be 0 or 1. However, if we didn't offset `time` by `i * 0.628318`, we'd end up with one circle that was 10 times brighter. Note that the center of the canvas, where all 10 circles intersect, is much brighter:

{% include rc-day-4/fragment-shader-demo-canvas.html demoNumber=6 %}

Why are we multipling `i` by 0.628318? Because 0.628318 = $\frac{2\pi}{10}$!

If you look at the rendering above, you'll see that we're drawing 10 circles inscribed in an invisible outer circle, and each circle is equidistant from the two circles adjacent to it. An angle of 2π radians corresponds to a full circle, so you can divide a circle into 10 equal [circular sectors](https://en.wikipedia.org/wiki/Circular_sector) if each sector has an angle of $\frac{2\pi}{10}$ radians. So by rotating our original circle by `i * 0.628318` (by adding `vec2(c, s)` to `p`) when we draw the $i^{th}$ circle, we ensure equal spacing between the circles.

![radians in a circle]({{ site.url }}/assets/rc-day-4/circle_radians.gif "A full circle corresponds to an angle of 2π radians."){:.center-image}


## Refactoring

Finally, taking what we've learned, let's rewrite the shader to be a little more readable:

{% highlight glsl %}
#define PI 3.1415926535897932384626433832795
#define NUM_CIRCLES 10.0
#define CIRCLE_RADIUS_SQUARED 0.5
#define RING_RADIUS 0.0025

precision mediump float;
uniform vec2 resolution;
uniform float time;

void main() {
  vec2 ndcPosition = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);
  vec3 destColor = vec3(1.0, 0.7, 0.5); // pink
  float circlesMask = 0.0;
  for(float i = 0.0; i < NUM_CIRCLES; i++) {
      float s = sin(time + i * (2.0 * PI / NUM_CIRCLES)) * CIRCLE_RADIUS_SQUARED;
      float c = cos(time + i * (2.0 * PI / NUM_CIRCLES)) * CIRCLE_RADIUS_SQUARED;
      vec2 rotation = vec2(c, s);
      vec2 circleWithRotation = length(ndcPosition + rotation);
      circlesMask += RING_RADIUS / abs(circleWithRotation - CIRCLE_RADIUS_SQUARED);
  }
  gl_FragColor = vec4(vec3(destColor * circlesMask), 1.0);
}
{% endhighlight %}

{% include rc-day-4/fragment-shader-demo-scripts.html %}
